{
    var A = require('./ast');
}

/*
 * Entry
 */

Start
    = _ rules:(Rule)* {
        return A.rules(rules);
    }

/*
 * Helpers
 */

_
    = [ \t\r\n]*

non_zero_digit
    = [1-9]

digit
    = [0-9]

float
    = rhead:non_zero_digit rtail:digit* '.' ftail:digit+ {
        return head + rtail.join('') + '.' + ftail.join('');
    }

integer
    = head:non_zero_digit tail:digit* {
        return head + tail.join('');
    }

/*
 * Productions
 */

Rule
    = sel:Selector body:Block {
        return A.rule(sel, body);
    }

Selector
    = ch:Chunk _ frags:Fragment* {
        var result = [ch];
        for (var i = 0; i < frags.length; ++i) {
            result.push(frags[i][0]);
            result.push(frags[i][1]);
        }
        return result;
    }
    / frags:Fragment* {
        var result = [];
        for (var i = 0; i < frags.length; ++i) {
            result.push(frags[i][0]);
            result.push(frags[i][1]);
        }
        return result;
    }
    
Fragment
    = co:Combinator _ ch:Chunk _ {
        return [co, ch];
    }

Chunk
    = e:Element i:ID? c:Class* a:Attrib* p:Pseudo* {
        return e + (i || '') + c.join('') + a.join('') + p.join('');
    }
    / i:ID c:Class* a:Attrib* p:Pseudo* {
        return i + c.join('') + a.join('') + p.join('');
    }
    / Class+ Attrib* Pseudo* {
        return c.join('') + a.join('') + p.join('');
    }
    / Attrib+ Pseudo* {
        return a.join('') + p.join('');
    }
    / Pseudo+ {
        return p.join('');
    }

Element
    = '*' {
        return '*';
    }
    / head:[a-zA-Z] tail:[a-zA-Z0-9-]* {
        return head + tail.join('');
    }

ID
    = '#' s:Symbol {
        return '#' + s;
    }

Class
    = '.' s:Symbol {
        return '.' + s;
    }

Combinator
    = '>'   { return ' > '; }
    / '~'   { return ' ~ '; }
    / '+'   { return ' + '; }
    / '&'   { return ' & '; }

Attrib
    = '[' _ AttributeName _ AttributeOperator _ AttributeValue _ ']'
    / '[' _ AttributeName _ ']'

AttributeName
    = Symbol

AttributeOperator
    = '='
    / '~='
    / '^='
    / '$='
    / '*='
    / '|='

AttributeValue
    = Symbol

Pseudo
    = '::' p:DoublePseudo   { return '::' + p; }
    / ':' p:SinglePseudo    { return ':' + p; }

SinglePseudo
    = 'root'
    / 'nth-child(' _ n:NTerm _ ')'
    / 'nth-last-child(' _ n:NTerm _ ')'
    / 'nth-of-type(' _ n:NTerm _ ')'
    / 'nth-last-of-type(' _ n:NTerm _ ')'
    / 'first-child'
    / 'last-child'
    / 'first-of-type'
    / 'last-of-type'
    / 'only-child'
    / 'only-of-type'
    / 'empty'
    / 'link'
    / 'visited'
    / 'active'
    / 'hover'
    / 'focus'
    / 'target'
    / 'lang(' Symbol ')'
    / 'enabled'
    / 'disabled'
    / 'checked'

DoublePseudo
    = 'first-line'
    / 'first-letter'
    / 'before'
    / 'after'

NTerm
    = Formula
    / 'odd'
    / 'even'
    / integer

Formula
    = a:integer 'n' plus:('+' b:integer)? {
        return (a + 'n+') + (plus ? b : '0');
    }

Block
    = '{' _ body:BlockInner* '}' _ {
        return body;
    }

BlockInner
    = Rule
    / Pair

Pair
    = prop:Property ':' _ values:Value+ ';' _ {
        return A.pair(prop, values)
    }

Property
    = name:[a-zA-Z-]+ _ {
        return name.join('');
    }

/*
 * TODO: string, URL, function call, expression
 */
Value
    = n:Number u:Unit {
        return A.value(n, u);
    }
    / Number
    / Symbol
    / ',' _
    / Variable

Number
    = Float
    / Integer

Integer
    = v:integer { return parseInt(v, 10); }

Float
    = v:float { return parseFloat(v); }
    
Unit
    = 'cm'
    / 'mm'
    / 'in'
    / 'pt'
    / 'px'
    / 'pc'
    / 'deg'
    / 'rad'

Symbol
    = head:[a-zA-Z-] tail:[a-zA-Z0-9_-]* _ {
        return head + tail.join('');
    }

Variable
    = '$' head:[a-zA-Z_] tail:[a-zA-Z9-9_]* {
        return A.variable(head + tail.join(''));
    }
